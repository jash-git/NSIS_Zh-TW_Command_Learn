<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
"http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
<html xmlns='http://www.w3.org/1999/xhtml'>


<!-- Mirrored from omega.idv.tw/nsis/SectionC.5.html by HTTrack Website Copier/3.x [XR&CO'2014], Tue, 27 Sep 2022 06:29:33 GMT -->
<head>
<title>取得 Windows 版本</title>
<meta name="generator" content="Halibut version 1.0 (NSIS Custom Build) xhtml-backend" />
<link rel="stylesheet" href="style.css" type='text/css' /><script language="JavaScript" type="text/javascript" src="chmlink.js"></script>
<meta http-equiv="Content-Type" content="text/html; charset=utf8">
</head>

<body>
<p><a href='SectionC.4.html'>上一頁</a> | <a href='Contents.html'>目錄</a> | <a href='SectionC.6.html'>下一頁</a></p>
<a name="C.5"></a>
<h2>C.5 取得 Windows 版本</h2>
<pre> ; GetWindowsVersion
 ;
 ; 基於 Yazno 的函數, http://yazno.tripod.com/powerpimpit/
 ; Joost Verburg 更新
 ;
 ; 返回到堆棧頂
 ;
 ; Windows 版本 (95, 98, ME, NT x.x, 2000, XP, 2003)
 ; 或
 ; '' (未知 Windows 版本)
 ;
 ; 用法:
 ;   Call GetWindowsVersion
 ;   Pop $R0
 ;   ; 在這裡 $R0 is &quot;NT 4.0&quot; 或其它
 
 Function GetWindowsVersion
 
   Push $R0
   Push $R1
  
   ClearErrors
 
   ReadRegStr $R0 HKLM \
   &quot;SOFTWARE\Microsoft\Windows NT\CurrentVersion&quot; CurrentVersion

   IfErrors 0 lbl_winnt
   
   ; 不是 NT
   ReadRegStr $R0 HKLM \
   &quot;SOFTWARE\Microsoft\Windows\CurrentVersion&quot; VersionNumber
 
   StrCpy $R1 $R0 1
   StrCmp $R1 '4' 0 lbl_error
 
   StrCpy $R1 $R0 3
 
   StrCmp $R1 '4.0' lbl_win32_95
   StrCmp $R1 '4.9' lbl_win32_ME lbl_win32_98
 
   lbl_win32_95:
     StrCpy $R0 '95'
   Goto lbl_done
 
   lbl_win32_98:
     StrCpy $R0 '98'
   Goto lbl_done
 
   lbl_win32_ME:
     StrCpy $R0 'ME'
   Goto lbl_done
 
   lbl_winnt:
 
   StrCpy $R1 $R0 1
 
   StrCmp $R1 '3' lbl_winnt_x
   StrCmp $R1 '4' lbl_winnt_x
 
   StrCpy $R1 $R0 3
 
   StrCmp $R1 '5.0' lbl_winnt_2000
   StrCmp $R1 '5.1' lbl_winnt_XP
   StrCmp $R1 '5.2' lbl_winnt_2003 lbl_error
 
   lbl_winnt_x:
     StrCpy $R0 &quot;NT $R0&quot; 6
   Goto lbl_done
 
   lbl_winnt_2000:
     Strcpy $R0 '2000'
   Goto lbl_done
 
   lbl_winnt_XP:
     Strcpy $R0 'XP'
   Goto lbl_done
 
   lbl_winnt_2003:
     Strcpy $R0 '2003'
   Goto lbl_done
 
   lbl_error:
     Strcpy $R0 ''
   lbl_done:
 
   Pop $R1
   Exch $R0
 
 FunctionEnd
</pre>
<p><a href='SectionC.4.html'>上一頁</a> | <a href='Contents.html'>目錄</a> | <a href='SectionC.6.html'>下一頁</a></p>

<hr />

<address>
</address>
</body>


<!-- Mirrored from omega.idv.tw/nsis/SectionC.5.html by HTTrack Website Copier/3.x [XR&CO'2014], Tue, 27 Sep 2022 06:29:33 GMT -->
</html>
